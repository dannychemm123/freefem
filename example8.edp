func u0 = 10. +90.*x/6.;
func k = 1.8*(y<0.5)+0.2;
real ue = 25.;
real alpha = 0.25;
real T = 5.;
real dt = 0.1;


mesh Th = square(30,5,[6.*x,y]);

fespace Vh(Th, P1);
Vh u = u0, v, uold;

problem thermic(u,v)
    = int2d(Th)(
        u*v/dt +k*(dx(u)*dx(v)+dy(u)*dy(v))
    )
    + int1d(Th, 1,3)(
        alpha*u*v

    )
    - int1d(Th, 1,3)(
        alpha*ue*v
    )
    - int2d(Th)(
        uold*v/dt
    )
    +on(2, 4, u = u0);

ofstream ff("temperature.dat");
for(real t = 0; t < T; t += dt){
    uold = u;
    thermic;
    ff << u(3., 0.5) << endl;
    plot(u,fill=1,value=1,nbiso=10,cmm="Temperature at t="+t);
}

for(int i = 0; i < 20; i++)
cout << dy(u)(6.0*i/20.0,0.9) << endl;
